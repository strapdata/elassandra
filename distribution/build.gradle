/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */


import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.taskdefs.condition.Os
import org.elasticsearch.gradle.BuildPlugin
import org.elasticsearch.gradle.EmptyDirTask
import org.elasticsearch.gradle.ConcatFilesTask
import org.elasticsearch.gradle.MavenFilteringHack
import org.elasticsearch.gradle.NoticeTask
import org.elasticsearch.gradle.precommit.DependencyLicensesTask
import org.elasticsearch.gradle.precommit.UpdateShasTask
import org.elasticsearch.gradle.test.RunTask

// for deb/rpm
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.netflix.nebula:gradle-ospackage-plugin:3.6.0'
  }
}

Collection distributions = project.subprojects.findAll {
   it.path.contains(':tools') == false && it.path.contains(':bwc') == false }

/*****************************************************************************
 *                  Third party dependencies report                          *
 *****************************************************************************/

// Concatenates the dependencies CSV files into a single file
task generateDependenciesReport(type: ConcatFilesTask) {
  files = fileTree(dir: project.rootDir,  include: '**/dependencies.csv' )
  headerLine = "name,version,url,license"
  target = new File(System.getProperty('csv')?: "${project.buildDir}/dependencies/es-dependencies.csv")
}

/*****************************************************************************
 *                                Notice file                                *
 *****************************************************************************/

// integ test zip only uses server, so a different notice file is needed there
task buildCoreNotice(type: NoticeTask) {
  licensesDir new File(project(':server').projectDir, 'licenses')
}

// other distributions include notices from modules as well, which are added below later
task buildFullNotice(type: NoticeTask) {
  licensesDir new File(project(':server').projectDir, 'licenses')
}

/*****************************************************************************
 *                                  Modules                                  *
 *****************************************************************************/

task buildModules(type: Sync) {
  into 'build/modules'
}

ext.restTestExpansions = [
  'expected.modules.count': 0,
]
// we create the buildModules task above so the distribution subprojects can
// depend on it, but we don't actually configure it until here so we can do a single
// loop over modules to also setup cross task dependencies and increment our modules counter
project.rootProject.subprojects.findAll { it.parent.path == ':modules' }.each { Project module ->
  buildFullNotice {
    def defaultLicensesDir = new File(module.projectDir, 'licenses')
    if (defaultLicensesDir.exists()) {
      licensesDir defaultLicensesDir
    }
  }
  buildModules {
    dependsOn({ project(module.path).bundlePlugin })
    into(module.name) {
      from { zipTree(project(module.path).bundlePlugin.outputs.files.singleFile) }
    }
  }
  // We would like to make sure integ tests for the distribution run after
  // integ tests for the modules included in the distribution.
  project.configure(distributions.findAll { it.name != 'integ-test-zip'}) { Project distribution ->
    distribution.afterEvaluate({
      // some integTest tasks will have multiple finalizers
      distribution.integTest.mustRunAfter module.tasks.find { t -> t.name.matches(".*integTest\$") }
    })
  }
  // also want to make sure the module's integration tests run after the integ-test-zip (ie rest tests)
  module.afterEvaluate({
    module.integTest.mustRunAfter(':distribution:integ-test-zip:integTest')
  })
  restTestExpansions['expected.modules.count'] += 1
}

// Integ tests work over the rest http layer, so we need a transport included with the integ test zip.
// All transport modules are included so that they may be randomized for testing
task buildTransportModules(type: Sync) {
  into 'build/transport-modules'
}

project.rootProject.subprojects.findAll { it.path.startsWith(':modules:transport-') }.each { Project transport ->
  buildTransportModules {
    dependsOn({ project(transport.path).bundlePlugin })
    into(transport.name) {
      from { zipTree(project(transport.path).bundlePlugin.outputs.files.singleFile) }
    }
  }
}

// make sure we have a clean task since we aren't a java project, but we have tasks that
// put stuff in the build dir
task customClean(type: Delete) {
    delete 'build'
}
clean.dependsOn customClean

configure(distributions) {
  /*****************************************************************************
   *                            Rest test config                               *
   *****************************************************************************/
  apply plugin: 'elasticsearch.standalone-rest-test'
  apply plugin: 'elasticsearch.rest-test'
  project.integTest {
    includePackaged project.name == 'integ-test-zip'
    if (project.name != 'integ-test-zip') {
      mustRunAfter ':distribution:integ-test-zip:integTest'
    }
  }
  project.integTestCluster {
    dependsOn project.assemble
    distribution = project.name
  }

  processTestResources {
    inputs.properties(project(':distribution').restTestExpansions)
    MavenFilteringHack.filter(it, project(':distribution').restTestExpansions)
  }

  /*****************************************************************************
   *                              Maven config                                 *
   *****************************************************************************/
  // note: the group must be correct before applying the nexus plugin, or it will capture the wrong value...
  project.group = "com.strapdata.elasticsearch.distribution.${project.name}"
  project.archivesBaseName = 'elassandra'

  // TODO: the map needs to be an input of the tasks, so that when it changes, the task will re-run...
  /*****************************************************************************
   *             Properties to expand when copying packaging files             *
   *****************************************************************************/
  project.ext {
    expansions = expansionsForDistribution(project.name)

    /*****************************************************************************
     *                   Common files in all distributions                       *
     *****************************************************************************/
    libFiles = copySpec {
      into 'lib'
      from { project(':server').jar }
      from { project(':server').configurations.runtime }
      from { project(':libs:plugin-classloader').jar }
      // delay add tools using closures, since they have not yet been configured, so no jar task exists yet
      from { project(':distribution:tools:launchers').jar }
      from { project(':distribution:tools:plugin-cli').jar }
      
      // Important files used to ship some cqlsh.py python dependencies if not already installed on host.
      // This prevents the need to run 'pip install cassandra driver'
      with copySpec {
        from '../../server/cassandra/lib'
        include '*.zip'
      }
      
      // Add dependency on cassandra-driver for UDF
      with copySpec {
        from '../../server/cassandra/lib'
        include 'cassandra-driver-core-3.0.1-shaded.jar'
      }
    }

    modulesFiles = copySpec {
      into 'modules'
      from project(':distribution').buildModules
    }

    transportModulesFiles = copySpec {
      into "modules"
      from project(':distribution').buildTransportModules
    }

    configFiles = copySpec {
      from '../src/main/resources/conf'
      MavenFilteringHack.filter(it, expansions)
    }

    binFiles = copySpec {
      from '../src/main/resources/bin2'
      eachFile { it.setMode(0755) }
      MavenFilteringHack.filter(it, expansions)
    }

    cassandraBinFiles = copySpec {
      from '../../server/cassandra/bin'
      exclude 'cassandra.in.sh'
      exclude 'cassandra'
      eachFile { it.setMode(0755) }
      MavenFilteringHack.filter(it, expansions)
    }

    homebinFiles = copySpec {
      from '../src/main/packaging/homebin'
      eachFile { it.setMode(0755) }
      MavenFilteringHack.filter(it, expansions)
    }

    pylibFiles = copySpec {
      from '../../server/cassandra/pylib'
    }

	cassandraLicenseFiles = copySpec {
      from '../../server/cassandra/lib/licenses'
    }
    
    sigarFiles = copySpec {
      from '../../server/cassandra/lib/sigar-bin'
    }

    toolsFiles = copySpec {
      from '../../server/cassandra/tools'
      exclude 'stress/**'
      eachFile { it.setMode(0755) }
    }

    stressFiles = copySpec {
      from '../../server/cassandra/build/tools'
    }

    commonFiles = copySpec {
      from rootProject.projectDir
      include 'LICENSE.txt'
      include 'CHANGES.txt'
      include 'README.md'
    }
    
    licenseFiles = copySpec {
    	from rootProject.buildDir.path + '/licenses'
    	include '**'
    }

    noticeFile = copySpec {
      if (project.name == 'integ-test-zip') {
        from buildCoreNotice
      } else {
        from buildFullNotice
      }
    }
  }

   /*****************************************************************************
   *                           Publishing setup                                *
   *****************************************************************************/
  if (['zip', 'integ-test-zip'].contains(it.name)) {
    BuildPlugin.configurePomGeneration(project)
    apply plugin: 'nebula.info-scm'
    apply plugin: 'nebula.maven-base-publish'
    apply plugin: 'nebula.maven-scm'
  }
}

/*****************************************************************************
 *                         Zip and tgz configuration                         *
 *****************************************************************************/
configure(distributions.findAll { ['zip', 'tar', 'integ-test-zip'].contains(it.name) }) {
  // CopySpec does not make it easy to create an empty director so we create the directory that we want, and then point CopySpec to its
  // parent to copy to the root of the distribution
  File plugins = new File(buildDir, 'plugins-hack/plugins')
  task createPluginsDir(type: EmptyDirTask) {
    dir "${plugins}"
    dirMode 0755
  }
  File data = new File(buildDir, 'data-hack/data')
  task createDataDir(type: EmptyDirTask) {
    dir "${data}"
    dirMode 0755
  }
  File logs = new File(buildDir, 'logs-hack/logs')
  task createLogDir(type: EmptyDirTask) {
    dir "${logs}"
    dirMode 0755
  }
  project.ext.archivesFiles = copySpec {
    into("elassandra-${project.version}") {
      with libFiles
      into('conf') {
        dirMode 0750
        fileMode 0660
        with configFiles
      }
      into('bin') {
        with cassandraBinFiles
        with copySpec {
          with binFiles
          from('../src/main/resources/bin2') {
            include '*.bat'
            filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance('crlf'))
          }
          MavenFilteringHack.filter(it, expansions)
        }
      }
      into('') {
        from {
          dirMode 0755
          logs.getParent()
        }
        from {
          dirMode 0755
          data.getParent()
        }
        from {
          dirMode 0755
          logs.getParent()
        }
      }
      into('pylib') {
        with pylibFiles
      }
      into('lib') {
         into('sigar-bin') {
            with sigarFiles
         }
         into('licenses') {
            dirMode 0755
      		with cassandraLicenseFiles
      		with licenseFiles
         }
      }
      into('tools') {
        with toolsFiles
        with stressFiles
      }
      into('') {
        from {
          dirMode 0755
          plugins.getParent()
        }
      }
      with commonFiles
      with noticeFile
      from('../src/main/resources') {
        include 'bin/*.exe'
      }
      if (project.name != 'integ-test-zip') {
        with modulesFiles
      } else {
        with transportModulesFiles
      }
    }
  }
}

/*****************************************************************************
 *                         Deb and rpm configuration                         *
 *****************************************************************************
 *
 * The general strategy here is to build a directory on disk, packagingFiles
 * that contains stuff that needs to be copied into the distributions. This is
 * important for two reasons:
 * 1. ospackage wants to copy the directory permissions that it sees off of the
 *    filesystem. If you ask it to create a directory that doesn't already
 *    exist on disk it petulantly creates it with 0755 permissions, no matter
 *    how hard you try to convince it otherwise.
 * 2. Convincing ospackage to pick up an empty directory as part of a set of
 *    directories on disk is reasonably easy. Convincing it to just create an
 *    empty directory requires more wits than I have.
 * 3. ospackage really wants to suck up some of the debian control scripts
 *    directly from the filesystem. It doesn't want to process them through
 *    MavenFilteringHack or any other copy-style action.
 *
 * The following commands are useful when it comes to check the user/group
 * and files permissions set within the RPM and DEB packages:
 *
 *    rpm -qlp --dump path/to/elasticsearch.rpm
 *    dpkg -c path/to/elasticsearch.deb
 */
configure(distributions.findAll { ['deb', 'rpm'].contains(it.name) }) {
  integTest.enabled = Os.isFamily(Os.FAMILY_WINDOWS) == false
  File packagingFiles = new File(buildDir, 'packaging')
  project.ext.packagingFiles = packagingFiles
  task processPackagingFiles(type: Copy) {
    from '../src/main/packaging'
    from 'src/main/packaging'

    MavenFilteringHack.filter(it, expansions)
    into packagingFiles
    /* Explicitly declare the outputs so that gradle won't skip this task if
      one of the other tasks like createEtc run first and create the packaging
      directory as a side effect. */
    outputs.dir("${packagingFiles}/env")
    outputs.dir("${packagingFiles}/systemd")
  }

  task createEtc(type: EmptyDirTask) {
    dir "${packagingFiles}/etc/cassandra"
    dirMode 0750
    outputs.dir dir
  }
  
  task createEtcScripts(type: EmptyDirTask) {
    dependsOn createEtc
    dir "${packagingFiles}/etc/cassandra/scripts"
    dirMode 0750
  }

  task fillEtc(type: Copy) {
    dependsOn createEtc, createEtcScripts
     with copySpec {
      with configFiles
      exclude '*.gcloud'
      exclude '*.ps1'
    }
    into "${packagingFiles}/etc/cassandra"
    /* Explicitly declare the output files so this task doesn't consider itself
      up to date when the directory is created, which it would by default. And
      that'll happen when createEtc runs. */ 
    outputs.file "${packagingFiles}/etc/cassandra/cassandra-env.sh"
    outputs.file "${packagingFiles}/etc/cassandra/cassandra-jaas.config"
    outputs.file "${packagingFiles}/etc/cassandra/cassandra-rackdc.properties"
    outputs.file "${packagingFiles}/etc/cassandra/cassandra-topology.properties"
    outputs.file "${packagingFiles}/etc/cassandra/cassandra.yaml"
    outputs.file "${packagingFiles}/etc/cassandra/commitlog_archiving.properties"
    outputs.file "${packagingFiles}/etc/cassandra/cqlshrc.sample"
    outputs.file "${packagingFiles}/etc/cassandra/elasticsearch.yml"
    outputs.file "${packagingFiles}/etc/cassandra/jvm.options"
    outputs.file "${packagingFiles}/etc/cassandra/logback-tools.xml"
    outputs.file "${packagingFiles}/etc/cassandra/logback.xml"
    outputs.file "${packagingFiles}/etc/cassandra/metrics-reporter-config-sample.yaml"
    outputs.file "${packagingFiles}/etc/cassandra/README.txt"
    outputs.file "${packagingFiles}/etc/cassandra/triggers/README.txt"
  }

  task createPidDir(type: EmptyDirTask) {
    dir "${packagingFiles}/var/run/cassandra"
  }
  task createLogDir(type: EmptyDirTask) {
    dir "${packagingFiles}/var/log/cassandra"
  }
  task createDataDir(type: EmptyDirTask) {
    dir "${packagingFiles}/var/lib/cassandra"
  }
  task createPluginsDir(type: EmptyDirTask) {
    dir "${packagingFiles}/usr/share/cassandra/plugins"
  }
  task createPluginsBinDir(type: EmptyDirTask) {
    dir "${packagingFiles}/usr/share/cassandra/bin"
  }

  task createTmpPylibDir(type: EmptyDirTask) {
    dir "${packagingFiles}/pylib"
  }
  task fillTmpPylibDir(type: Copy) {
    dependsOn createTmpPylibDir
    with pylibFiles
    into "${packagingFiles}/pylib"
  }

  task setupPylib(type: Exec) {
    dependsOn fillTmpPylibDir
    workingDir "${packagingFiles}/pylib"
    if (project.name == "rpm") {
      commandLine "python2", "setup.py", "install", "--no-compile",
                  "--root", "${packagingFiles}",
                  "--install-lib", "/usr/lib/python2.7/site-packages"
      outputs.dir "${packagingFiles}/usr/lib/python2.7/site-packages"
    }
    else if (project.name == "deb") {
      commandLine "python2", "setup.py", "install", "--no-compile",
                  "--root", "${packagingFiles}",
                  "--install-lib", "/usr/lib/python2.7/dist-packages"
      outputs.dir "${packagingFiles}/usr/lib/python2.7/dist-packages"
    }
  }

  /**
   * Setup the build/packaging directory to be like the target filesystem
   * because ospackage will use arbitrary permissions if you try to create a
   * directory that doesn't exist on the filesystem.
   */
  task preparePackagingFiles {
    dependsOn processPackagingFiles, fillEtc, createPidDir, createLogDir,
        createDataDir, createPluginsDir, createPluginsBinDir, setupPylib
  }

  apply plugin: 'nebula.ospackage-base'
  ospackage {
    packageName 'elassandra'
    maintainer 'Strapdata Team <contact@strapdata.com>'
    summary '''
      Elassandra is a fork of Elasticsearch modified to run as a plugin for Apache Cassandra \
      in a scalable and resilient peer-to-peer architecture.
      Elasticsearch code is embedded in Cassanda nodes providing advanced search features \
      on Cassandra tables and Cassandra serve as an Elasticsearch data and configuration store.
      Reference documentation can be found at http://doc.elassandra.io/
      '''.stripIndent().replace('\n', ' ').trim()
    url 'https://www.strapdata.com/'

    // signing setup
    if (project.hasProperty('signing.password') && System.getProperty('build.snapshot', 'true') == 'false') {
      signingKeyId = project.hasProperty('signing.keyId') ? project.property('signing.keyId') : 'D88E42B4'
      signingKeyPassphrase = project.property('signing.password')
      signingKeyRingFile = project.hasProperty('signing.secretKeyRingFile') ?
                           project.file(project.property('signing.secretKeyRingFile')) :
                           new File(new File(System.getProperty('user.home'), '.gnupg'), 'secring.gpg')
    }

    String scripts = "${packagingFiles}/scripts"
    preInstall file("${scripts}/preinst")
    postInstall file("${scripts}/postinst")
    preUninstall file("${scripts}/prerm")
    postUninstall file("${scripts}/postrm")

    if (project.name == 'rpm') {
      requires('/bin/bash')
      requires('python', '2.7', GREATER | EQUAL)
      requires('jre', "1.8.0", GREATER | EQUAL)
      recommends('ntp')
      conflicts('cassandra')
    } else if (project.name == 'deb') {
      requires('bash')
      requires('python', '2.7', GREATER | EQUAL)
      requires('openjdk-8-jre-headless').or("java8-runtime")
      recommends('ntp')
      conflicts('cassandra')
    }
    requires('coreutils')

    into('/usr/share/cassandra') {
    	  fileMode 0644
      dirMode 0755
      user 'root'
      permissionGroup 'root'
      with libFiles
      with modulesFiles
      with copySpec {
        with commonFiles
        if (project.name == 'deb') {
          // Deb gets a copyright file instead.
          exclude 'LICENSE.txt'
        }
      }
      with homebinFiles
      into('lib/sigar-bin') {
        with copySpec {
          with sigarFiles
          exclude '*.dll'
        }
      }
      into('lib/licenses') {
         dirMode 0755
         with cassandraLicenseFiles
         with licenseFiles
      }
      into('tools') {
        with copySpec {
          with toolsFiles
          with stressFiles
          exclude 'bin/*.bat'
          exclude 'bin/*.exe'
          exclude 'bin/*.ps1'
        }
      }
      with noticeFile
    }
    
    configurationFile '/etc/cassandra/cassandra-env.sh'
    configurationFile '/etc/cassandra/cassandra-jaas.config'
    configurationFile '/etc/cassandra/cassandra-rackdc.properties'
    configurationFile '/etc/cassandra/cassandra-topology.properties'
    configurationFile '/etc/cassandra/cassandra.yaml'
    configurationFile '/etc/cassandra/commitlog_archiving.properties'
    configurationFile '/etc/cassandra/cqlshrc.sample'
    configurationFile '/etc/cassandra/elasticsearch.yml'
    configurationFile '/etc/cassandra/jvm.options'
    configurationFile '/etc/cassandra/logback-tools.xml'
    configurationFile '/etc/cassandra/logback.xml'
    configurationFile '/etc/cassandra/metrics-reporter-config-sample.yaml'
    configurationFile '/etc/cassandra/README.txt'
    
        into('/usr/bin') {
      fileMode 0755
      user 'root'
      permissionGroup 'root'
      with copySpec {
        with cassandraBinFiles
        with binFiles
        exclude '*.bat'
        exclude '*.exe'
        exclude '*.ps1'
        exclude 'aliases.sh'
        exclude 'cassandra.in.sh'
      }
    }

    into('/etc/cassandra') {
      dirMode 0755
      fileMode 0664
      permissionGroup 'cassandra'
      includeEmptyDirs true
      createDirectoryEntry true
      fileType CONFIG | NOREPLACE
      from "${packagingFiles}/etc/cassandra"

      exclude "**/triggers"
      exclude "**/triggers/README.txt"
    }
    
    into('/etc/cassandra/triggers') {
      dirMode 0750
      fileMode 0664
      permissionGroup 'cassandra'
      includeEmptyDirs true
      createDirectoryEntry true
      fileType CONFIG | NOREPLACE
      from "${packagingFiles}/etc/cassandra/triggers"
    }

    into('/usr/lib/tmpfiles.d') {
      fileMode 0644
      from "${packagingFiles}/systemd/cassandra.conf"
    }
    configurationFile '/usr/lib/systemd/system/cassandra.service'
    into('/usr/lib/systemd/system') {
    	  fileMode 0644
      fileType CONFIG | NOREPLACE
      from "${packagingFiles}/systemd/cassandra.service"
    }
    into('/usr/lib/sysctl.d') {
    	  fileMode 0644
      fileType CONFIG | NOREPLACE
      from "${packagingFiles}/systemd/sysctl/cassandra.conf"
    }
    configurationFile '/etc/init.d/cassandra'
    into('/etc/init.d') {
      fileMode 0750
      fileType CONFIG | NOREPLACE
      from "${packagingFiles}/init.d/cassandra"
    }
    configurationFile project.expansions['path.env']
    into(new File(project.expansions['path.env']).getParent()) {
      fileType CONFIG | NOREPLACE
      fileMode 0664
      from "${project.packagingFiles}/env/cassandra"
    }

    /**
     * Suck up all the empty directories that we need to install into the path.
     */
    Closure suckUpEmptyDirectories = { path, u, g, mode ->
      into(path) {
        from "${packagingFiles}/${path}"
        includeEmptyDirs true
        createDirectoryEntry true
        user u
        permissionGroup g
        dirMode mode
        fileMode mode
      }
    }
    suckUpEmptyDirectories('/var/run', 'cassandra', 'cassandra', 0755)
    suckUpEmptyDirectories('/var/log', 'cassandra', 'cassandra', 0750)
    suckUpEmptyDirectories('/var/lib', 'cassandra', 'cassandra', 0750)
    suckUpEmptyDirectories('/usr/share/cassandra', 'root', 'root', 0755)
    if (project.name == 'rpm') {
      // suckUpEmptyDirectories('/usr/lib/python2.7/site-packages/cqlshlib', 'root', 'root', 0755)
      into('/usr/lib/python2.7/site-packages') {
        from "${packagingFiles}/usr/lib/python2.7/site-packages"
        fileMode 0664
      }
      directory("/usr/lib/python2.7/site-packages/cqlshlib", 0755)
    }
    else if (project.name == 'deb') {
      //suckUpEmptyDirectories('/usr/lib/python2.7/dist-packages/cqlshlib', 'root', 'root', 0755)
      into('/usr/lib/python2.7/dist-packages') {
        from "${packagingFiles}/usr/lib/python2.7/dist-packages"
        fileMode 0664
      }
      directory("/usr/lib/python2.7/dist-packages/cqlshlib", 0755)
    }
  }
}

task run(type: RunTask) {
  distribution = 'zip'
}

/**
 * Build some variables that are replaced in the packages. This includes both
 * scripts like bin/elasticsearch and bin/elasticsearch-plugin that a user might run and also
 * scripts like postinst which are run as part of the installation.
 *
 * <dl>
 *  <dt>package.name</dt>
 *  <dd>The name of the project. Its sprinkled throughout the scripts.</dd>
 *  <dt>package.version</dt>
 *  <dd>The version of the project. Its mostly used to find the exact jar name.
 *    </dt>
 *  <dt>path.conf</dt>
 *  <dd>The default directory from which to load configuration. This is used in
 *    the packaging scripts, but in that context it is always
 *    /etc/elasticsearch. Its also used in bin/elasticsearch-plugin, where it is
 *    /etc/elasticsearch for the os packages but $ESHOME/config otherwise.</dd>
 *  <dt>path.env</dt>
 *  <dd>The env file sourced before bin/elasticsearch to set environment
 *    variables. Think /etc/defaults/elasticsearch.</dd>
 *  <dt>heap.min and heap.max</dt>
 *  <dd>Default min and max heap</dd>
 *  <dt>scripts.footer</dt>
 *  <dd>Footer appended to control scripts embedded in the distribution that is
 *    (almost) entirely there for cosmetic reasons.</dd>
 *  <dt>stopping.timeout</dt>
 *  <dd>RPM's init script needs to wait for elasticsearch to stop before
 *    returning from stop and it needs a maximum time to wait. This is it. One
 *    day. DEB retries forever.</dd>
 * </dl>
 */
Map<String, String> expansionsForDistribution(distributionType) {
  final String defaultHeapSize = "2g" // not used by elassandra
  final String packagingPathData = "path.data: /var/lib/cassandra"
  final String pathLogs = "/var/log/cassandra"
  final String packagingPathLogs = "path.logs: ${pathLogs}"
  final String packagingLoggc = "${pathLogs}/gc.log"

  String footer = "# Built for ${project.name}-${project.version} " +
      "(${distributionType})"
  Map<String, Object> expansions = [
    'project.name': project.name,
    'project.version': version,

    'path.conf': [
      'tar': '"$ES_HOME"/conf',
      'zip': '"$ES_HOME"/conf',
      'integ-test-zip': '"$ES_HOME"/conf',
      'def': '/etc/cassandra',
    ],
    'path.data': [
      'deb': packagingPathData,
      'rpm': packagingPathData,
      'def': '#path.data: /var/lib/cassandra/data'
    ],
    'path.env': [
      'deb': '/etc/default/cassandra',
      'rpm': '/etc/sysconfig/cassandra',
      /* There isn't one of these files for tar or zip but its important to
        make an empty string here so the script can properly skip it. */
      'def': 'if [ -z "$ES_PATH_CONF" ]; then ES_PATH_CONF="$CASSANDRA_HOME"/conf; done',
    ],
    'source.path.env': [
       'deb': 'source /etc/default/cassandra',
       'rpm': 'source /etc/sysconfig/cassandra',
       'def': 'if [ -z "$ES_PATH_CONF" ]; then ES_PATH_CONF="$ES_HOME"/conf; fi',
    ],
    'path.logs': [
      'deb': packagingPathLogs,
      'rpm': packagingPathLogs,
      'def': '#path.logs: /path/to/logs'
    ],
    'loggc': [
      'deb': packagingLoggc,
      'rpm': packagingLoggc,
      'def': 'logs/gc.log'
    ],

	/* Elassandra hack in order to make elasticsearch-plugin works */
    'path.plugin_eshome': [
      'deb': '/usr/share/cassandra',
      'rpm': '/usr/share/cassandra',
      'def': '$(dirname $(dirname "$SCRIPT"))'
    ],
    
    'heap.min': defaultHeapSize,
    'heap.max': defaultHeapSize,

    'heap.dump.path': [
      'deb': "-XX:HeapDumpPath=/var/lib/cassandra",
      'rpm': "-XX:HeapDumpPath=/var/lib/cassandra",
      'def': "#-XX:HeapDumpPath=/heap/dump/path"
    ],

    'stopping.timeout': [
      'rpm': 86400,
    ],

    'scripts.footer': [
      /* Debian needs exit 0 on these scripts so we add it here and preserve
        the pretty footer. */
      'deb': "exit 0\n${footer}",
      'def': footer
    ],
  ]
  Map<String, String> result = [:]
  expansions = expansions.each { key, value ->
    if (value instanceof Map) {
      // 'def' is for default but its three characters like 'rpm' and 'deb'
      value = value[distributionType] ?: value['def']
      if (value == null) {
        return
      }
    }
    result[key] = value
  }
  return result
}
